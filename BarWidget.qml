import QtQuick
import qs.Commons
import qs.Ui

BarWidget {
  id: root
  moduleName: "ucmz851.omatorrent"

  function injectPanel() {
    var target = panelLoader.item
    if (!target) return
    if ("bar" in target) target.bar = root.bar
    if ("settings" in target) target.settings = root.settings
    if ("anchorItem" in target) target.anchorItem = button
    if ("hostWidget" in target) target.hostWidget = root
  }

  function refresh() {
    if (panelLoader.item && panelLoader.item.refresh) panelLoader.item.refresh()
  }

  function togglePanel() {
    if (panelLoader.item && panelLoader.item.toggle) panelLoader.item.toggle()
  }

  readonly property bool opened: panelLoader.item ? panelLoader.item.opened === true : false

  function open() {
    if (panelLoader.item && panelLoader.item.open) panelLoader.item.open()
  }

  function close() {
    if (panelLoader.item && panelLoader.item.close) panelLoader.item.close()
  }

  readonly property bool popoutSwitchClosing: panelLoader.item ? panelLoader.item.popoutSwitchClosing === true : false

  function closeForPopoutSwitch() {
    if (panelLoader.item) panelLoader.item.closeForPopoutSwitch()
  }

  function getStatusColor(panelItem) {
    if (root.opened) return Color.accent
    if (panelItem && panelItem.isSearching) return "#e5c07b"
    if (panelItem && panelItem.qbConnected && panelItem.qbGlobal && panelItem.qbGlobal.active_downloads > 0) return "#87c095"
    if (panelItem && panelItem.resultsCount > 0) return "#87c095"
    return root.bar ? root.bar.barForeground : Color.foreground
  }

  implicitWidth: button.implicitWidth
  implicitHeight: button.implicitHeight

  onBarChanged: injectPanel()
  onSettingsChanged: injectPanel()

  Loader {
    id: panelLoader
    active: true
    source: Qt.resolvedUrl("Panel.qml")
    visible: false
    onLoaded: {
      root.injectPanel()
      Qt.callLater(root.injectPanel)
    }
  }

  BarIconButton {
    id: button
    anchors.fill: parent
    bar: root.bar
    text: "󰚌"
    foreground: getStatusColor(panelLoader.item)
    slotSize: Style.bar.statusSlot
    tooltipText: {
      var item = panelLoader.item
      if (!item) return "OmaTorrent: Search & Transfers"
      if (item.qbConnected && item.qbGlobal && item.qbGlobal.active_downloads > 0) {
        return "OmaTorrent: " + item.qbGlobal.active_downloads + " downloading (" + item.qbGlobal.dl_speed_str + " 󰜮)"
      }
      if (item.lastQuery) {
        return "OmaTorrent: " + item.resultsCount + " results for '" + item.lastQuery + "'"
      }
      return "OmaTorrent: Multi-Source Search & qBittorrent Monitor"
    }

    onPressed: function(b) {
      if (!root.bar) return
      if (b === Qt.MiddleButton) root.refresh()
      else root.togglePanel()
    }
  }
}
